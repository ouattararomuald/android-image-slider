// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

  ext.buildConfig = [
    'compileSdk': 28,
    'minSdk': 16,
    'targetSdk': 28,
  ]

  ext.versions = [
    'androidx': '1.0.0',
    'dokka': '0.9.17',
    'glide': '4.8.0',
    'groupie': '2.3.0',
    'kotlin': '1.3.11',
  ]

  ext.deps = [
    'androidx': [
      'appcompat': 'androidx.appcompat:appcompat:1.0.2',
      'constraintLayout': 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2',
      'material': 'com.google.android.material:material:1.1.0-alpha01',
      'annotations': "androidx.annotation:annotation:${versions.androidx}",
      'exifinterface': "androidx.exifinterface:exifinterface:${versions.androidx}",
      'recyclerview': "androidx.recyclerview:recyclerview:${versions.androidx}",
      'test': [
        'runner': 'androidx.test:runner:1.1.0',
        'espresso': [
          'core': 'androidx.test.espresso:espresso-core:3.1.0',
        ],
      ]
    ],
    'kotlin': [
      'stdlib': [
        'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      ],
    ],
    'groupie': [
      'core': "com.xwray:groupie:${versions.groupie}",
      'kotlinExtensions': "com.xwray:groupie-kotlin-android-extensions:${versions.groupie}",
    ],
    'junit': 'junit:junit:4.12',
    'pageTransformer': 'com.github.miaoyongjun:PageTransformer:1.0',
    'glide': [
      'runtime': "com.github.bumptech.glide:glide:${versions.glide}",
      'compiler': "com.github.bumptech.glide:compiler:${versions.glide}",
    ],
    'picasso': 'com.squareup.picasso:picasso:2.71828',
    'truth': 'com.google.truth:truth:0.42',
  ]

  ext.isCi = "true" == System.getenv('CI')

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.4.0-alpha02'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.dokka}"
  }
}

plugins {
  id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC7-2'
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
  repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
  }
}

subprojects { project ->
  repositories {
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME

  task copyResourcesToClasses(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/intermediates/classes/test/debug/resources"
  }

  afterEvaluate {
    if (project.tasks.findByName('assembleDebug')) {
      assembleDebug.dependsOn(copyResourcesToClasses)
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

detekt {
  version = "1.0.0.RC7-2"
  profile("sample") {
    input = "$projectDir/sample/src/main/java"
    config = "$projectDir/code-analysis/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
  }
  profile("slider") {
    input = "$projectDir/slider-library/src/main/java"
    config = "$projectDir/code-analysis/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
  }
}
