// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

  ext.buildConfig = [
    'compileSdk': 28,
    'minSdk': 16,
    'targetSdk': 28,
  ]

  ext.versions = [
    'androidx': '1.0.0',
    'dokka': '0.9.18',
    'glide': '4.9.0',
    'groupie': '2.3.0',
    'kotlin': '1.3.21',
  ]

  ext.deps = [
    'androidx': [
      'appcompat': 'androidx.appcompat:appcompat:1.1.0-alpha04',
      'constraintLayout': 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3',
      'material': 'com.google.android.material:material:1.1.0-alpha05',
      'annotations': 'androidx.annotation:annotation:1.1.0-beta01',
      'exifinterface': 'androidx.exifinterface:exifinterface:1.1.0-alpha01',
      'recyclerview': 'androidx.recyclerview:recyclerview:1.1.0-alpha04',
      'core': [
        'ktx': "androidx.core:core-ktx:1.1.0-alpha05"
      ],
      'test': [
        'runner': 'androidx.test:runner:1.1.2-alpha02',
        'espresso': [
          'core': 'androidx.test.espresso:espresso-core:3.2.0-alpha02',
        ],
      ]
    ],
    'kotlin': [
      'stdlib': [
        'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      ],
    ],
    'groupie': [
      'core': "com.xwray:groupie:${versions.groupie}",
      'kotlinExtensions': "com.xwray:groupie-kotlin-android-extensions:${versions.groupie}",
    ],
    'junit': 'junit:junit:4.12',
    'pageTransformer': 'com.github.miaoyongjun:PageTransformer:1.0',
    'glide': [
      'runtime': "com.github.bumptech.glide:glide:${versions.glide}",
      'compiler': "com.github.bumptech.glide:compiler:${versions.glide}",
    ],
    'picasso': 'com.squareup.picasso:picasso:2.71828',
    'truth': 'com.google.truth:truth:0.42',
  ]

  ext.isCi = "true" == System.getenv('CI')

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.0-alpha09'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.dokka}"
  }
}

plugins {
  id 'com.github.ben-manes.versions' version '0.21.0'
  id 'io.gitlab.arturbosch.detekt' version '1.0.0-RC14'
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
  repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    google()
    jcenter()
  }
}

subprojects { project ->
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME

  task copyResourcesToClasses(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/intermediates/classes/test/debug/resources"
  }

  afterEvaluate {
    if (project.tasks.findByName('assembleDebug')) {
      assembleDebug.dependsOn(copyResourcesToClasses)
    }
  }

  if (project.name == 'slider-library') {
    apply plugin: 'org.jetbrains.dokka-android'

    task androidDokka(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
      outputFormat = 'jekyll'
      outputDirectory = "$buildDir/documentation"

      reportUndocumented = false
      skipDeprecated = true
      jdkVersion = 8

      externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/android/support/packages")
        packageListUrl =
                new URL("https://developer.android.com/reference/android/support/package-list")
      }

      externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/androidx/packages")
        packageListUrl = new URL("https://developer.android.com/reference/androidx/package-list")
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

detekt {
  version = "1.0.0-RC14"

  input = files(
    "$projectDir/sample/src/main/java",
    "$projectDir/slider-library/src/main/java",
    "$projectDir/glide-loader/src/main/java",
    "$projectDir/picasso-loader/src/main/java"
  )

  config = files("$projectDir/code-analysis/detekt.yml")
  filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
}
