// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

  ext.buildConfig = [
    'compileSdk': 'android-28',
    'minSdk': 16,
    'targetSdk': 28,
  ]

  ext.versions = [
    'dokka': '0.9.17',
    'groupie': '2.2.0-alpha1',
    'kotlin': '1.2.50',
    'robolectric': '3.8',
    'support': '28.0.0-alpha3',
  ]

  ext.deps = [
    'android': [
      'support': [
        'appcompatV7': "com.android.support:appcompat-v7:${versions.support}",
        'constraintLayout': 'com.android.support.constraint:constraint-layout:1.1.1',
        'design': "com.android.support:design:${versions.support}",
        'annotations': "com.android.support:support-annotations:${versions.support}",
        'exifinterface': "com.android.support:exifinterface:${versions.support}",
        'recyclerview': "com.android.support:recyclerview-v7:${versions.support}",
        'test': [
          'runner': 'com.android.support.test:runner:1.0.2',
          'espresso': [
            'core': 'com.android.support.test.espresso:espresso-core:3.0.2',
          ],
        ]
      ],
      'robolectric': [
        'core': "org.robolectric:robolectric:${versions.robolectric}",
      ],
    ],
    'kotlin': [
      'stdlib': [
        'jdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
      ],
    ],
    'groupie': [
      'core': "com.xwray:groupie:${versions.groupie}",
      'kotlinExtensions': "com.xwray:groupie-kotlin-android-extensions:${versions.groupie}",
    ],
    'junit': 'junit:junit:4.12',
    'pageTransformer': 'com.github.miaoyongjun:PageTransformer:1.0',
    'picasso': 'com.squareup.picasso:picasso:2.71828',
    'truth': 'com.google.truth:truth:0.40',
  ]

  ext.isCi = "true" == System.getenv('CI')

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.2.0-alpha18'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.dokka}"
  }
}

plugins {
  id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC7-2'
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
  repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
  }
}

subprojects { project ->
  repositories {
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME

  task copyResourcesToClasses(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/intermediates/classes/test/debug/resources"
  }

  afterEvaluate {
    if (project.tasks.findByName('assembleDebug')) {
      assembleDebug.dependsOn(copyResourcesToClasses)
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

detekt {
  version = "1.0.0.RC7-2"
  profile("sample") {
    input = "$projectDir/sample/src/main/java"
    config = "$projectDir/code-analysis/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
  }
  profile("slider") {
    input = "$projectDir/slider/src/main/java"
    config = "$projectDir/code-analysis/detekt.yml"
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
  }
}
