// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

  ext.buildConfig = [
    'compileSdk': 29,
    'minSdk': 16,
    'targetSdk': 29,
  ]

  ext.versions = [
    'dokka': '0.9.18',
    'glide': '4.11.0',
    'groupie': '2.3.0',
    'kotlin': '1.3.61',
  ]

  ext.deps = [
    'androidx': [
      'appcompat': 'androidx.appcompat:appcompat:1.2.0-alpha02',
      'constraintLayout': 'androidx.constraintlayout:constraintlayout:2.0.0-beta4',
      'material': 'com.google.android.material:material:1.2.0-alpha04',
      'annotations': 'androidx.annotation:annotation:1.1.0',
      'exifinterface': 'androidx.exifinterface:exifinterface:1.2.0-beta01',
      'recyclerview': 'androidx.recyclerview:recyclerview:1.2.0-alpha01',
      'core': [
        'ktx': "androidx.core:core-ktx:1.3.0-alpha01"
      ],
      'test': [
        'runner': 'androidx.test:runner:1.3.0-alpha03',
        'espresso': [
          'core': 'androidx.test.espresso:espresso-core:3.2.0-alpha02',
        ],
      ]
    ],
    'kotlin': [
      'stdlib': [
        'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      ],
    ],
    'groupie': [
      'core': "com.xwray:groupie:${versions.groupie}",
      'kotlinExtensions': "com.xwray:groupie-kotlin-android-extensions:${versions.groupie}",
    ],
    'junit': 'junit:junit:4.13',
    'pageTransformer': 'com.github.miaoyongjun:PageTransformer:1.0',
    'glide': [
      'runtime': "com.github.bumptech.glide:glide:${versions.glide}",
      'compiler': "com.github.bumptech.glide:compiler:${versions.glide}",
    ],
    'picasso': 'com.squareup.picasso:picasso:2.71828',
    'truth': 'com.google.truth:truth:1.0.1',
  ]

  ext.isCi = "true" == System.getenv('CI')

  repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.0.0-alpha09'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.dokka}"
  }
}

plugins {
  id 'com.github.ben-manes.versions' version '0.27.0'
  id 'io.gitlab.arturbosch.detekt' version '1.5.0'
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
  repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://plugins.gradle.org/m2/' }
    google()
    jcenter()
  }
}

subprojects { project ->
  repositories {
    mavenCentral()
    google()
    jcenter {
      content {
        // just allow to include kotlinx projects
        // detekt needs 'kotlinx-html' for the html report
        includeGroup "org.jetbrains.kotlinx"
      }
    }
  }

  group = GROUP
  version = VERSION_NAME

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
      jvmTarget = '1.8'
    }
  }

  task copyResourcesToClasses(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/intermediates/classes/test/debug/resources"
  }

  afterEvaluate {
    if (project.tasks.findByName('assembleDebug')) {
      assembleDebug.dependsOn(copyResourcesToClasses)
    }
  }

  if (project.name == 'slider-library') {
    apply plugin: 'org.jetbrains.dokka-android'

    task androidDokka(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
      outputFormat = 'jekyll'
      outputDirectory = "$buildDir/documentation"

      reportUndocumented = false
      skipDeprecated = true
      jdkVersion = 8

      externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/android/support/packages")
        packageListUrl =
                new URL("https://developer.android.com/reference/android/support/package-list")
      }

      externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/androidx/packages")
        packageListUrl = new URL("https://developer.android.com/reference/androidx/package-list")
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

detekt {
  toolVersion = '1.5.0'
  config = files("$projectDir/code-analysis/detekt.yml")
  //filters = ".*test.*,.*/resources/.*,.*/tmp/.*"

  input = files(
    "$projectDir/sample/src/main/java",
    "$projectDir/slider-library/src/main/java",
    "$projectDir/glide-loader/src/main/java",
    "$projectDir/picasso-loader/src/main/java"
  )
}

//tasks {
//  withType<Detekt> {
//    // Target version of the generated JVM bytecode. It is used for type resolution.
//    this.jvmTarget = "1.8"
//  }
//}
